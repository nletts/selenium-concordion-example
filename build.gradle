defaultTasks 'clean', 'build'

apply plugin: 'java'
apply plugin: 'eclipse'

repositories {
	mavenLocal()
    mavenCentral()
}

dependencies {
  testCompile 'junit:junit:4.12'

  testCompile 'org.concordion:concordion:1.4.7'

  testCompile 'org.seleniumhq.selenium:selenium-java:2.45.0' 
  testCompile 'org.seleniumhq.selenium:selenium-api:2.45.0'
  testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.45.0'  
  testCompile 'org.seleniumhq.selenium:selenium-chrome-driver:2.45.0'  
  testCompile 'com.codeborne:phantomjsdriver:1.2.1'

  testCompile 'org.springframework:spring-core:4.1.4.RELEASE'
  testCompile 'org.springframework:spring-tx:4.1.4.RELEASE'
  testCompile 'org.springframework:spring-context:4.1.4.RELEASE'
  testCompile 'org.springframework:spring-test:4.1.4.RELEASE'

  testCompile 'log4j:log4j:1.2.17'
}



test {
    systemProperty "concordion.output.dir", "${reporting.baseDir}"
    //Uncomment to use a specific browser
    //systemProperty "spring.profiles.active", "firefox"
}

sourceSets {
    test {
        resources {
            srcDir 'src/test/java'
        }
    }
}

task headless(type: Test, dependsOn: 'clean') {
    doFirst {
        systemProperty "spring.profiles.active", "phantomjs"
    }
}

def webDrivers = [ "chrome", "firefox" ];

webDrivers.each { def targetName -> 
    task "driver-$targetName" (type: Test, dependsOn: 'clean') { 
        doFirst {
		    systemProperty "concordion.output.dir", "${reporting.baseDir}/$targetName"
		    systemProperty "spring.profiles.active", "$targetName"
		}
    } 
} 

//Runs all defined browser web drivers
task regression(dependsOn: tasks.matching { Task task -> task.name.startsWith("driver-")})


